logger.quiet "Java version: ${System.properties['java.version']}"
logger.quiet "Gradle version: $gradle.gradleVersion"

buildscript {
  repositories {
    maven {
      url 'https://plugins.gradle.org/m2/'
    }
  }
  dependencies {
    classpath group: 'com.diffplug.spotless', name: 'spotless-plugin-gradle', version: '5.12.5'
    classpath group: 'gradle.plugin.com.github.spotbugs.snom', name: 'spotbugs-gradle-plugin', version: '4.7.1'
    classpath group: 'net.rdrei.android.buildtimetracker', name: 'gradle-plugin', version: '0.11.1'
  }
}

// Build time tracker
apply plugin: 'build-time-tracker'
buildtimetracker {
  reporters {
    summary {
      ordered false
      threshold 50
      barstyle 'ascii'
      shortenTaskNames false
    }
  }
}

allprojects {
  group 'com.wilmol'
  version = '1.0.0'
  repositories {
    mavenCentral()
  }
}

subprojects {
  apply plugin: 'java'
  sourceCompatibility = 16.0
  targetCompatibility = 16.0

  // Spotless (code formatting/linting)
  apply plugin: 'com.diffplug.spotless'
  spotless {
    java {
      removeUnusedImports()
      googleJavaFormat()
    }
  }

  // Checkstyle (static analysis - code quality/style)
  apply plugin: 'checkstyle'
  checkstyle {
    toolVersion = '8.42'
    configFile = rootProject.file('./checkstyle.xml')
    maxErrors = 0
    maxWarnings = 0
    ignoreFailures = false
  }

  // SpotBugs (static analysis - find possible bugs, performance issues etc.)
  apply plugin: 'com.github.spotbugs'
  spotbugs {
    effort = 'max'
    reportLevel = 'low'
    ignoreFailures = false
    excludeFilter = rootProject.file('./spotbugs-exclude.xml')
  }
  tasks.withType(com.github.spotbugs.snom.SpotBugsTask) {
    reports {
      html.enabled = true
      xml.enabled = false
    }
  }

  // Tests
  tasks.withType(Test) {
    // run tests in parallel, assumes they're threadsafe
    maxParallelForks = Runtime.runtime.availableProcessors().intdiv(2) ?: 1
    // use JUnit 5 engine
    useJUnitPlatform()
    testLogging {
      events = ['failed', 'skipped']
      // log the full failure messages
      exceptionFormat = 'full'
      showExceptions = true
      showCauses = true
      showStackTraces = true
      // log the overall results (based on https://stackoverflow.com/a/36130467/6122976)
      afterSuite { desc, result ->
        if (!desc.parent) { // will match the outermost suite
          println("Results: ${result.resultType} " +
              "(${result.testCount} test${result.testCount > 1 ? "s" : ""}, " +
              "${result.successfulTestCount} passed, " +
              "${result.failedTestCount} failed, " +
              "${result.skippedTestCount} skipped)")
        }
      }
    }
  }

  // JaCoCo (code coverage reporting)
  apply plugin: 'jacoco'
  jacocoTestReport {
    reports {
      xml.enabled = true
      html.enabled = true
      csv.enabled = false
    }
  }
  check.dependsOn jacocoTestReport

  // pin dependency versions
  ext {
    // production
    guavaVersion = '30.1.1-jre'
    log4jVersion = '2.14.1'

    // test
    junitVersion = '5.7.2'
    truthVersion = '1.1.3'
    mockitoVersion = '3.12.1'
  }

  dependencies {
    implementation group: 'com.google.guava', name: 'guava', version: "$guavaVersion"
    implementation group: 'org.apache.logging.log4j', name: 'log4j-core', version: "$log4jVersion"
    implementation group: 'com.github.spotbugs', name: 'spotbugs-annotations', version: '4.2.3'

    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter', version: "$junitVersion"
    testImplementation group: 'com.google.truth', name: 'truth', version: "$truthVersion"
    testImplementation group: 'com.google.truth.extensions', name: 'truth-java8-extension', version: "$truthVersion"
    testImplementation group: 'org.mockito', name: 'mockito-core', version: "$mockitoVersion"
    testImplementation group: 'org.mockito', name: 'mockito-junit-jupiter', version: "$mockitoVersion"
    testImplementation group: 'org.mockito', name: 'mockito-inline', version: "$mockitoVersion"
  }

  // dependency cleanup, exclusions and resolutions
  configurations.all {
    exclude group: 'org.assertj' // using truth instead

    resolutionStrategy {
      force "com.google.guava:guava:$guavaVersion" // so the android version isn't pulled in
    }
  }
}
